plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.7.2' // Check for the latest version
    id "xyz.jpenilla.run-paper" version '2.3.1'
    id 'org.mangorage.build-tools' version '+'
}

group = 'org.mangorage'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://maven.fabricmc.net/"
    }
    flatDir {
        dirs 'libs'
        dirs 'run/versions/1.21.1'
    }
}

dependencies {
    remapper("net.fabricmc:tiny-remapper:0.10.1:fat")
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")

    implementation project(":SigmaCoreMixins")

    if (useMango) {
        boot(project(":ServerBoot"))
        ignite('libs:ignite')
        server('libs:paper-1.21.1')
    }

    compileOnly "space.vectrix.ignite:ignite-api:1.1.0"
    compileOnly "net.fabricmc:sponge-mixin:0.15.2+mixin.0.8.7"
    compileOnly "io.github.llamalad7:mixinextras-common:0.4.1"
}

SigmaGradle {
    setServer("server")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Copy the subproject jar after it has been built
task copySigmaMixinsJarForBuild(type: Copy) {
    // This block will only be executed once the task is called, ensuring the subproject jar is built
    from { project(':SigmaCoreMixins').tasks.jar.archiveFile }
    into "$buildDir/resources/main/mixinjar"
}

// Ensure the copySubprojectJar task runs after the subproject jar task is completed
copySigmaMixinsJarForBuild.dependsOn(':SigmaCoreMixins:jar')

// Ensure the resources task depends on the copying of the subproject jar
processResources.dependsOn(copySigmaMixinsJarForBuild)